MODULE main
VAR
  c : 0..3; -- Number of cannibals that are not on the target side.
  m : 0..3; -- Number of missionaries that are not on the target side.
  b : 0..1; -- Number of boats (max. 1) that are not on the target side.
  tc : 0..3;
  tm : 0..3;

  mb : 0..2; -- Number of missionaries on the b.
  cb : 0..2; -- Number of cannibals on the b.

INVAR mb + cb > 0  -- There is always someone on the boat (no run wasted).
INVAR mb + cb <= 2 -- Boat has a maximum capacity of 2 people.

-- In the initial state, everything is not on the target side.
ASSIGN
  init(c) := 3;
  init(m) := 3;
  init(b) := 0;

  init(tc) := 0;
  init(tm) := 0;

-- The boat is not on the target side. This means that in the next side:
--  1. The boat will be on the target side (it travels the river).
--  2. The number of people on the target side will increase respectively.
TRANS b=0 ->
  next(b) = 1 &
  next(c) = c - cb &
  next(m) = m - mb &
  next(tm) = tm + mb &
  next(tc) = tc + cb

-- Analogous.
TRANS b=1 ->
  next(b) = 0 &
  next(c) = c + cb &
  next(m) = m + mb &
  next(tm) = tm - mb &
  next(tc) = tc - cb

-- Goal is reached if everyone is on the target side. Safety is met if there are
-- always more missionaries than cannibals on either side.
DEFINE
  safe := ((m < 3) -> (c >= m)) & ((m > 0) -> (c <= m));
  goal := tc = 3 & tm = 3;

SPEC ! E[safe U goal];
LTLSPEC ! (safe U goal);

